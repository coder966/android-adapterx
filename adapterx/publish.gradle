apply plugin:'com.github.dcendents.android-maven'

/**
 * These variables must be defined here. The plugin -com.github.dcendents.android-maven- accesses them.
 */
group = project.getProperty('group_id')
version = project.getProperty('version')

/**
 * The pom.xml file is generated by the plugin. However, it omits a lot of info. Add them here.
 */
install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name project.getProperty('pkg_name')
                description project.getProperty('pkg_desc')
                url project.getProperty('pkg_website_url')
                groupId project.getProperty('group_id')
                artifactId project.getProperty('artifact_id')
                version project.getProperty('version')
                licenses {
                    license {
                        name project.getProperty('pkg_license')
                        url project.getProperty('pkg_license_url')
                    }
                }
                developers {
                    developer {
                        id project.getProperty('dev_id')
                        name project.getProperty('dev_name')
                        email project.getProperty('dev_email')
                    }
                }
                scm {
                    connection project.getProperty('pkg_vcs_url')
                    developerConnection project.getProperty('pkg_vcs_url')
                    url project.getProperty('pkg_vcs_url')
                }
            }
        }
    }
}

/**
 * Creates sourcesJar
 */
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

/**
 * Creates javadoc. Needed for javadocJar
 */
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

/**
 * Creates javadocJar
 */
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

/**
 * Adds javadocJar and sourcesJar to archives which is used below "configurations = ['archives']"
 */
artifacts {
    archives javadocJar
    archives sourcesJar
}

apply plugin:'com.jfrog.bintray'

bintray {
    user = project.getProperty('bintrayUser')
    key = project.getProperty('bintrayApiKey')
    configurations = ['archives']
    // override = true // default is false
    pkg {
        repo = project.getProperty('pkg_repo')
        name = project.getProperty('pkg_name')
        description = project.getProperty('pkg_desc')
        websiteUrl = project.getProperty('pkg_website_url')
        vcsUrl = project.getProperty('pkg_vcs_url')
        licenses = [project.getProperty('pkg_license')]
        publish = true
        publicDownloadNumbers = false
        version {
            name = project.getProperty('version')
        }
    }
}
